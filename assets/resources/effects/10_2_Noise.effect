// Copyright (c) 2017-2018 Xiamen Yaji Software Co., Ltd.  

// Note: Current format version is experiment, the format may be changed.
// The future format may not be compatible, you may need to update the script manually.

// 注意：当前版本的格式是实验性的，之后还会进行修改。
// 后续版本的格式不保证兼容当前格式，可能需要手动升级到最新版本。，
%{
  techniques: [
    {
      passes: [
        {
          vert: vs
          frag: fs
          cullMode: none
          blend: true
        }
      ]
      layer: 0
    }
  ]
  properties: {
    texture: {
      type: sampler2D
      value: null
    }
    u_time: {
      type: number
      value: 0
    }
    u_resolution: {
      type: vec2
      value: [640,640]
    }
  }
%}

%% vs {

precision highp float;

uniform mat4 cc_matViewProj;

attribute vec3 a_position;
attribute lowp vec4 a_color;
attribute mediump vec2 a_uv0;

varying mediump vec2 v_uv0;
varying lowp vec4 v_color;

void main () {
  mat4 mvp;
  
  mvp = cc_matViewProj;
  
  v_uv0 = a_uv0;
  v_color = a_color;

  gl_Position = mvp * vec4(a_position, 1);
}

}

%% fs {

precision mediump float;

#define PI 3.14159265359
#define TWO_PI 6.28318530718

uniform sampler2D texture;
uniform vec2 u_resolution;
uniform float u_time;

varying mediump vec2 v_uv0;
varying lowp vec4 v_color;

float random(vec2 st) {
  return fract(sin(dot(st.xy, vec2(12.9898,78.233)))*43758.5453123);
}

float noise(vec2 st) {
  vec2 i = floor(st);
  vec2 f = fract(st);

  // Four corners in 2D of a tile
  float a = random(i);
  float b = random(i+vec2(1.0,0.0));
  float c = random(i+vec2(0.0,1.0));
  float d = random(i+vec2(1.0,1.0));

  vec2 u = f * f * (3.0 - 2.0 * f);
  return mix(a,b,u.x) + (c-a)*u.y*(1.0-u.x) + (d-b)*u.x*u.y;
}

#define OCTAVES 6

float fbm(vec2 st) {
  float v = 0.0;
  float a = 0.5;
  vec2 shift = vec2(100.0);
  // Rotate to reduce axial bias
  mat2 rot = mat2(cos(0.5),sin(0.5),-sin(0.5),cos(0.5));

  for (int i=0; i<OCTAVES; i++) {
    v += a * noise(st);
    st = rot * st * 2.0 + shift;
    a *= .5;
  }

  return v;
}

void main () {
  vec2 st = gl_FragCoord.xy / u_resolution * 3.0;
  //st.x *= u_resolution.x / u_resolution.y;

  vec3 color = vec3(.0);
  //color = texture2D(texture, v_uv0).rgb;

  vec2 q = vec2(0.);
  q.x = fbm(st + 0.0*u_time);
  q.y = fbm(st + vec2(1.0));

  vec2 r = vec2(0.);
  r.x = fbm(st + 1.0*q + vec2(1.7,9.2) + 0.15*u_time);
  r.y = fbm(st + 1.0*q + vec2(8.3,2.8) + 0.126*u_time);

  float f = fbm(st+r);

  color = mix(vec3(0.101961,0.619608,0.666667), 
              vec3(0.666667,0.666667,0.498039),
              clamp((f*f)*4.0,0.0,1.0));
  color = mix(color, vec3(0,0,0.164706), clamp(length(q),0.0,1.0));
  color = mix(color, vec3(0.666667,1,1), clamp(length(r.x),0.0,1.0));

  gl_FragColor = vec4((f*f*f+.6*f*f+.5*f)*color, 1.0);
}

}
